buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:2.0.0'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
//apply plugin: 'maven-publish'

// This is the version of this library used when deploying the artifact to jcenter
version = '1.0.25'
group = 'io.cogswell'
def siteUrl = 'https://cogswell.io/'
def gitUrl = 'https://github.com/cogswell-io/cogs-android-client-sdk'
Properties settingsProps = new Properties()
settingsProps.load(new File('settings.gradle').newDataInputStream())
def packageName =  settingsProps.getProperty('rootProject.name').replaceAll("'","")

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 23
  }

  lintOptions {
    abortOnError false
  }
}

install {
  repositories.mavenInstaller {
    pom {
      project {
        packaging 'aar'

        name 'Cogs Android Client SDK'
        url siteUrl

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id = 'joeledwards'
            name = 'Joel Edwards'
            email = 'jedwards@aviatainc.com'
          }
        }

        scm {
          connection gitUrl
          developerConnection gitUrl
          url siteUrl
        }
      }
    }
  }
}

def props = new Properties()
try {
  props.load(project.rootProject.file('local.properties').newDataInputStream())
} catch (Exception e) {
  println 'Could not load local.properties'
}

bintray {
  dryRun = false
  def userProp = props.getProperty('bintray_user')
  def apiKeyProp = props.getProperty('bintray_api_key')
  user = userProp == null ? System.getenv('BINTRAY_USER') : userProp
  key = apiKeyProp == null ? System.getenv('BINTRAY_API_KEY') : apiKeyProp

  configurations = ['archives']

  pkg {
    repo = 'maven'
    userOrg = user
    name = packageName
    desc = 'Cogs Android Client SDK'
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = ["Apache-2.0"]
    labels = ['iot', 'message broker', 'big data']
    publish = System.getenv('BINTRAY_PUBLISH') == 'true' 

    version {
      vcsTag = version
    }
  }

  println 'Publishing '+pkg.userOrg+'/'+pkg.repo+'/'+pkg.name+'/'+version+' NOTE: '+(publish?'This package will be automatically published':'Someone will need to manually publish this package at bintray.com')
}

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  compile 'org.json:json:20160212'
  compile 'com.google.android.gms:play-services:8.3.0'
  compile 'commons-codec:commons-codec:1.10'
  compile 'com.squareup.okhttp3:okhttp:3.2.0' // We're using a specific version because Android 4.4.2 does not support DELETE requests with body data.
  compile 'com.koushikdutta.async:androidasync:2.1.7'
  compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.12'
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

